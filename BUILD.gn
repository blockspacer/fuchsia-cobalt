# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")

config("cobalt_config") {
  include_dirs = [
    "//garnet/public/lib/syslog/cpp",
    "$cobalt_root",
    "$root_gen_dir/third_party/cobalt",
    "//zircon/system/ulib/syslog/include",
  ]
}

proto_library("cobalt_proto") {
  sources = [
    "clearcut_extensions.proto",
    "encrypted_message.proto",
    "envelope.proto",
    "event.proto",
    "key.proto",
    "observation.proto",
    "observation2.proto",
    "observation_batch.proto",
  ]
  import_dirs = [ "//third_party/protobuf/src" ]
  generate_python = false
  cc_generator_options = "lite"
  deps = [
    "$cobalt_root/third_party/clearcut:clearcut_proto",
    "//third_party/boringssl",
  ]
}

group("logging") {
  if (is_fuchsia) {
    public_deps = [
      "//garnet/public/lib/syslog/cpp",
    ]
  }

  if (is_linux) {
    public_deps = [
      "//third_party/glog",
    ]
  }
}

group("tracing") {
  if (is_fuchsia) {
    public_deps = [
      "//zircon/public/lib/trace",
    ]
  }
}

executable("cobalt_core_unittests") {
  testonly = true

  output_name = "cobalt_core_tests"

  deps = [
    "config:tests",
    "encoder:unittests",
    "logger:tests",
    "//third_party/boringssl:crypto",
    "//third_party/googletest:gtest_main",
  ]
}

if (is_fuchsia) {
  import("//build/test/test_package.gni")

  test_package("cobalt_core_tests") {
    deps = [
      ":cobalt_core_unittests",
    ]

    tests = [
      {
        name = "cobalt_core_tests"
      },
    ]
  }
}
