# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_binary.gni")
import("//build/go/go_library.gni")
import("//build/go/go_test.gni")

go_library("config") {
  name = "config"
  source_dir = "$root_gen_dir/go-proto-gen/src/$cobalt_root/config"
  non_go_deps = [ "$cobalt_root/config:cobalt_registry_proto" ]
}

_source_packages = [ "validator" ]
foreach(pkg, _source_packages) {
  go_library(pkg) {
    name = pkg
    source_dir = pkg
  }

  go_test("${pkg}_test") {
    gopackages = [ pkg ]
    deps = [
      ":$pkg",
      ":config",
      "//garnet/public/go/third_party:github.com/golang/glog",
      "//garnet/public/go/third_party:github.com/golang/protobuf",
    ]
  }
}

# This duplicates some source paths, but main needs to be relocated into a
# package path for our go_build rules.
go_library("main") {
  name = "main"

  # sources = ["config_change_validator_main.go"]
  source_dir = "."
}

go_binary("bin") {
  gopackage = "main"
  output_name = "config_change_validator"

  deps = [
    ":config",
    ":main",
    "//garnet/public/go/third_party:github.com/golang/glog",
    "//garnet/public/go/third_party:github.com/golang/protobuf",
  ]
  foreach(pkg, _source_packages) {
    deps += [ ":$pkg" ]
  }
}

group("tests") {
  testonly = true

  deps = []
  foreach(pkg, _source_packages) {
    deps += [ ":${pkg}_test" ]
  }
}
